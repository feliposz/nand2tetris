// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Check if x or y must be set to zero
    Mux16(a=x, b=false, sel=zx, out=x1);
    Mux16(a=y, b=false, sel=zy, out=y1);
    // Negate both
    Not16(in=x1, out=x1Not);
    Not16(in=y1, out=y1Not);
    // Now select which to use in the next step
    Mux16(a=x1, b=x1Not, sel=nx, out=x2);
    Mux16(a=y1, b=y1Not, sel=ny, out=y2);
    // Do both functions (add, and)
    Add16(a=x2, b=y2, out=addOut);
    And16(a=x2, b=y2, out=andOut);
    // Select which result to output
    Mux16(a=andOut, b=addOut, sel=f, out=fOut);
    // Negate output
    Not16(in=fOut, out=fOutNot);
    // Select which output to use (negated or original)
    Mux16(a=fOut, b=fOutNot, sel=no, out=out,
        out[15]=ng, // Use high bit to check for negative
        out[0..7]=out8Low, out[8..15]=out8High); // Split output in 8-bit segments
    // Check if any bit is set in both segments
    Or8Way(in=out8Low, out=lowBitSet);
    Or8Way(in=out8High, out=highBitSet);
    Or(a=lowBitSet, b=highBitSet, out=bitSet);
    // If no bits are set, then set zero flag to true
    Not(in=bitSet, out=zr);
}