// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {

    static Array ram;
    static Array freeList;
    static boolean debug;

    /** Initializes the class. */
    function void init() {
        let ram = 0;
        let freeList = 2048;
        let freeList[0] = null; // next
        let freeList[1] = 14336; // size
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return ram[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let ram[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var Array node, best, block;
        var int i;
        do Memory._freeList(65);
        let best = null;
        let node = freeList;
        while (~(node = null)) {
            if (node[1] = (size + 2)) { // perfect fit
                let best = node;
                let node = null;
            } else {
                if (node[1] > (size + 2)) {
                    if (best = null) { // first found
                        let best = node;
                    } else {
                        if (node[1] < best[1]) { // found a smaller segment
                            let best = node;
                        }
                    }
                }
                let node = node[0]; // advance to next
            }
        }
        if (best = null) { // no free node found
            do Sys.error(6);
            return -1;
        } else {
            let block = best + best[1] - (size + 2); // allocate block at end of free space
            let block[0] = null; // next
            let block[1] = size; // size
            let best[1] = best[1] - (size + 2); // shrink free space
            return block + 2; // allocated address start after next and size
        }
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        var Array curr;
        var boolean break;
        // insert at head of freeList
        if ((o-2) < freeList) {
            let o[-2] = freeList;
            let freeList = o-2;
        } else {
            let curr = freeList;
            // find fist node that is after o
            while (~break) {
                if (curr[0] = null) {
                    let break = true;
                } else {
                    if ((o-2) > curr[0]) {
                        let curr = curr[0];
                    } else {
                        let break = true;
                    }
                }
            }
            // insert freed node between curr and next
            let o[-2] = curr[0];
            let curr[0] = o-2;
        }
        do Memory._freeList(68);
        return;
    }    

    function void _freeList(char c) {
        var Array node;
        if (~debug) {
            return;
        }
        let node = freeList;
        do Output.printChar(c);
        do Output.printChar(58);
        while (~(node = null)) {
            do Output.printInt(node);
            do Output.printChar(40);
            do Output.printInt(node[1]);
            do Output.printChar(41);
            do Output.printChar(45);
            do Output.printChar(62);
            let node = node[0];
        }
        do Output.printChar(88);
        do Output.println();
        return;
    }

    function void _setDebug() {
        let debug = true;
        return;
    }
}
