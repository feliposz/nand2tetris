// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */
class Memory {

    static Array ram;
    static Array freeList;
    static boolean debug;

    /** Initializes the class. */
    function void init() {
        let ram = 0;
        let freeList = 2048;
        let freeList[0] = null; // next
        let freeList[1] = 14336; // size
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return ram[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let ram[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var Array best, block;
        var int i;
        let best = Memory._bestFit(size);
        if (best = null) { // first attempt found no free nodes
            do Memory._compact(); // try compacting blocks to free some space
            let best = Memory._bestFit(size);
        }
        if (best = null) { // no free node found
            do Sys.error(6);
            return -1;
        } else {
            let block = best + best[1] - size; // allocate block at end of free space
            let block[0] = null; // next
            let block[1] = size; // size
            let best[1] = best[1] - (size + 2); // shrink free space
            do Memory._printDebug(65);
            do Output.printInt(block+2);
            do Output.println();
            return block + 2; // allocated address start after next and size
        }
    }

    function int _bestFit(int size) {
        var Array best, node;
        let best = null;
        let node = freeList;
        while (~(node = null)) {
            if (node[1] = (size + 2)) { // perfect fit
                let best = node;
                let node = null;
            } else {
                if (node[1] > (size + 2)) {
                    if (best = null) { // first found
                        let best = node;
                    } else {
                        if (node[1] < best[1]) { // found a smaller segment
                            let best = node;
                        }
                    }
                }
                let node = node[0]; // advance to next
            }
        }
        return best;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        var Array node;
        var boolean break;
        // insert at head of freeList
        if ((o-2) < freeList) {
            let o[-2] = freeList;
            let freeList = o-2;
        } else {
            // find first node that is after o
            let node = freeList;
            while (~break) {
                if (node[0] = null) {
                    let break = true;
                } else {
                    if ((o-2) > node[0]) {
                        let node = node[0];
                    } else {
                        let break = true;
                    }
                }
            }
            // insert freed node between current and next
            let o[-2] = node[0];
            let node[0] = o-2;
        }
        do Memory._printDebug(68);
        return;
    }

    function void _compact() {
        var Array node, next;
        // compact free list by merging contiguous blocks
        do Memory._printDebug(109);
        let node = freeList;
        while (~(node[0] = null)) {
            let next = node[0];
            if ((node + node[1] + 2) = next) { // is contiguous
                // merge blocks
                let node[1] = node[1] + next[1] + 2; // increase size of current
                let node[0] = next[0];
            } else {
                let node = next; // advance
            }
        }
        do Memory._printDebug(77);
        return;
    }

    function void _printDebug(char c) {
        var Array node;
        if (~debug) {
            return;
        }
        let node = freeList;
        do Output.printChar(c);
        do Output.printChar(58);
        while (~(node = null)) {
            do Output.printInt(node);
            do Output.printChar(40);
            do Output.printInt(node[1]);
            do Output.printChar(41);
            do Output.printChar(45);
            do Output.printChar(62);
            let node = node[0];
        }
        do Output.printChar(88);
        do Output.println();
        return;
    }

    function void _startDebug() {
        let debug = true;
        do Memory._printDebug(73);
        return;
    }

    function void _stopDebug() {
        let debug = false;
        return;
    }
}
