class Logo {

    field boolean quit, error;
    field String message;
    field Turtle turtle;

    constructor Logo new() {
        let turtle = Turtle.new();
        return this;
    }

    method void dispose() {
        do turtle.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var String input;
        do cmdClear();
        while (~quit) {
            let input = promptUser();
            do parseInput(input);
            do input.dispose();
        }
        do StringUtil.printConstant("Goodbye! :D");
        return;
    }

    method String promptUser() {
        // Clear message/prompt area
        do Screen.setColor(false);
        do Screen.drawRectangle(0, 232, 511, 255);
        if (error) {
            do Output.moveCursor(21, 0);
            if (message) {
                do Output.printString(message);
                do message.dispose();
                let message = null;
            }
        }
        do Output.moveCursor(22, 0);
        return Keyboard.readLine("?");
    }

    method void parseInput(String input) {
        var int len, i, tokens, arg, sign;
        var boolean break;
        var String cmd;

        let len = input.length();

        // allow multiple commands per line
        while (i < len) {

            let cmd = String.new(len-i);

            // skip space
            let break = false;
            while ((i < len) & ~break) {
                if (input.charAt(i) = 32) {
                    let i = i + 1;
                } else {
                    let break = true;
                }
            }

            // get command
            let break = false;
            while ((i < len) & ~break) {
                if ((input.charAt(i) > 64) & (input.charAt(i) < 91)) { // letter A-Z
                    do cmd.appendChar(input.charAt(i));
                    let i = i + 1;
                } else {
                    // invalid character
                    let break = true;
                }
            }

            // skip space
            let break = false;
            while ((i < len) & ~break) {
                if (input.charAt(i) = 32) {
                    let i = i + 1;
                } else {
                    let break = true;
                }
            }

            // parse numeric argument
            let arg = 0;
            let break = false;
            if (i < len) {
                if (input.charAt(i) = 45) { // minus sign
                    let sign = -1;
                    let i = i + 1;
                } else {
                    let sign = 1;
                }
                while ((i < len) & ~break) {
                    if ((input.charAt(i) > 47) & (input.charAt(i) < 58)) { // digit 0-9
                        let arg = (arg * 10) + (input.charAt(i) - 48);
                        let i = i + 1;
                    } else {
                        // invalid digit
                        let break = true;
                    }
                }
                let arg = arg * sign;
            }

            if (StringUtil.equalsConstant(cmd, "REPEAT")) {
                let i = processRepeat(arg, input, i);
            } else {
                do processCommand(cmd, arg);
            }

            //debug
            // do StringUtil.printConstant("i=");
            // do Output.printInt(i);
            // do StringUtil.printConstant(" cmd=");
            // do Output.printString(cmd);
            // do StringUtil.printConstant(" arg=");
            // do Output.printInt(arg);
            // do Output.println();

            do cmd.dispose();

            // skip rest of line on error
            if (error) {
                let i = len;
            }
        }
        return;
    }

    method int processRepeat(int times, String input, int i) {
        var int brackets, len;
        var boolean break;
        var String body;

        let len = input.length();
        let body = String.new(len - i);

        // skip space
        let break = false;
        while ((i < len) & ~break) {
            if (input.charAt(i) = 32) {
                let i = i + 1;
            } else {
                let break = true;
            }
        }

        // parse repeat body
        if (input.charAt(i) = 91) { // skip first [
            let i = i + 1;
            let brackets = brackets + 1;
            while ((i < len) & (brackets > 0)) {
                if (input.charAt(i) = 91) { // [ inside body
                    let brackets = brackets + 1;
                } else {
                    if (input.charAt(i) = 93) { // ] inside body
                        let brackets = brackets - 1;
                    }
                }
                if (brackets > 0) { // skip last ]
                    do body.appendChar(input.charAt(i));
                }
            let i = i + 1;
            }
        } else {
            let error = true;
            let message = "expected [";
        }

        if (brackets > 0) {
            let error = true;
            let message = "expected ]";
        } else {
            while (times > 0) {
                do parseInput(body);
                if (error) {
                    let times = 0;
                } else {
                    let times = times - 1;
                }
            }
        }

        do body.dispose();
        return i;
    }

    method void processCommand(String cmd, int arg) {
        let error = true;
        if (StringUtil.equalsConstant(cmd, "QUIT")) {
            let quit = true;
            let error = false;
        }
        if (StringUtil.equalsConstant(cmd, "HELP")) {
            do cmdHelp();
        }
        if (StringUtil.equalsConstant(cmd, "DEMO")) {
            do runDemo();
        }
        if (StringUtil.equalsConstant(cmd, "CS")) {
            do cmdClear();
        }
        if (StringUtil.equalsConstant(cmd, "TEST")) {
            do test();
        }
        if (StringUtil.equalsConstant(cmd, "FD")) {
            do cmdForward(arg);
        }
        if (StringUtil.equalsConstant(cmd, "BK")) {
            do cmdBack(arg);
        }
        if (StringUtil.equalsConstant(cmd, "RT")) {
            do cmdRight(arg);
        }
        if (StringUtil.equalsConstant(cmd, "LT")) {
            do cmdLeft(arg);
        }
        if (StringUtil.equalsConstant(cmd, "PD")) {
            do turtle.penDown();
            let error = false;
        }
        if (StringUtil.equalsConstant(cmd, "PU")) {
            do turtle.penUp();
            let error = false;
        }
        if (error) {
            let message = "Unknown command (type HELP to learn commands)";
        }
        return;
    }

    method void cmdHelp() {
        do Screen.clearScreen();
        do Output.moveCursor(0, 0);
        do StringUtil.printConstant("HELP"); do Output.println();
        do StringUtil.printConstant("----"); do Output.println();
        do Output.println();
        do StringUtil.printConstant("HELP          Show this help screen"); do Output.println();
        do StringUtil.printConstant("DEMO          Demonstration mode"); do Output.println();
        do StringUtil.printConstant("QUIT          Quit program"); do Output.println();
        do Output.println();
        do StringUtil.printConstant("CS            Clear screen"); do Output.println();
        do StringUtil.printConstant("FD distance   Move forward"); do Output.println();
        do StringUtil.printConstant("BK distance   Move backwards"); do Output.println();
        do StringUtil.printConstant("RT angle      Turn right"); do Output.println();
        do StringUtil.printConstant("LT angle      Turn left"); do Output.println();
        do StringUtil.printConstant("PD            Pen down (start drawing)"); do Output.println();
        do StringUtil.printConstant("PU            Pen up (stop drawing)"); do Output.println();
        do Output.println();
        do StringUtil.printConstant("REPEAT times [ commands ]"); do Output.println();
        do StringUtil.printConstant("              Repeat the list of commands inside brackets"); do Output.println();
        do Output.println();
        do StringUtil.printConstant("*NOTE* It is possible to enter more than one command per line."); do Output.println();

        do Output.println();
        do StringUtil.printConstant("Press any key to continue..."); do Output.println();
        do Keyboard.readChar();
        do cmdClear();
        let error = false;
        return;
    }

    method void cmdClear() {
        do Screen.clearScreen();
        do turtle.reset();
        do turtle.draw();
        let error = false;
        return;
    }

    method void test() {
        var int a, count, sides, i;
        do Screen.clearScreen();
        do turtle.reset();
        // draw a test pattern
        // do Output.moveCursor(0, 0);
        // let count = 23 * 64;
        // while (count > 0) {
        //     do Output.printChar(65 + (count&31));
        //     let count = count - 1;
        // }
        // do turtle._saveFrame();
        let sides = 3;
        while (sides < 20) {
            do cmdClear();
            do Output.moveCursor(3, 0);
            do Output.printInt(sides);
            // do turtle.penUp();
            // do turtle.setXY(-250 + (sides * 10), 0);
            // do turtle.setH(0);
            // do turtle.draw();
            // do turtle.penDown();
            let i = 0;
            while (i < sides) {
                do turtle.forward(300 / sides);
                do turtle.draw();
                do turtle.right(360 / sides);
                do turtle.draw();
                let a = a + (360 / sides);
                let i = i + 1;
            }
            let sides = sides + 1;
            do turtle._debug();
            do Keyboard.readChar();
        }
        if (false) {
            do turtle.setXY(0, 0);
            do turtle.setH(0);
            let a = 0;
            while (a < 37) {
                do turtle.forward(10);
                do turtle.draw();
                do turtle.left(10);
                //do Keyboard.readChar();
                let a = a + 1;
            }
            do turtle.setXY(0, 0);
            do turtle.setH(0);
            let a = 0;
            while (a < 10) {
                do turtle.right(90);
                do turtle.forward(20);
                do turtle.draw();
                do turtle.left(90);
                do turtle.forward(20);
                do turtle.draw();
                let a = a + 1;
            }
        }
        if (false) {
            do turtle.right(45);
            do turtle.forward(50);
            do turtle.draw();
            do turtle.left(90);
            do turtle.forward(50);
            do turtle.draw();
        }
        let quit = true;
        let error = false;
        return;
    }

    method void hello() {
        do StringUtil.printConstant("Hello, world! ");
        if (Keyboard.keyPressed() = 140) {
            let quit = true;
        }
        return;
    }

    method void cmdForward(int dist) {
        if ((dist < 0) | (dist > 320)) {
            let message = "Sorry, I can't move that much. :(";
            let error = true;
        } else {
            do turtle.forward(dist);
            do turtle.draw();
            let error = false;
        }
        return;
    }

    method void cmdBack(int dist) {
        if ((dist < 0) | (dist > 320)) {
            let message = "Sorry, I can't move that much. :(";
            let error = true;
        } else {
            do turtle.back(dist);
            do turtle.draw();
            let error = false;
        }
        return;
    }

    method void cmdRight(int angle) {
        if ((angle < 0) | (angle > 360)) {
            let message = "Sorry, I can't turn that much. :(";
            let error = true;
        } else {
            do turtle.right(angle);
            do turtle.draw();
            let error = false;
        }
        return;
    }

    method void cmdLeft(int angle) {
        if ((angle < 0) | (angle > 360)) {
            let message = "Sorry, I can't turn that much. :(";
            let error = true;
        } else {
            do turtle.left(angle);
            do turtle.draw();
            let error = false;
        }
        return;
    }

    method void runDemo() {
        var Array demo;
        var String input;
        var int size, i;
        let size = 37;
        let demo = Array.new(size);
        let demo[0] = "CS";
        let demo[1] = "LT 90";
        let demo[2] = "FD 100";
        let demo[3] = "RT 90";
        let demo[4] = "FD 100";
        let demo[5] = "RT 90";
        let demo[6] = "FD 25";
        let demo[7] = "RT 90";
        let demo[8] = "FD 75";
        let demo[9] = "LT 90";
        let demo[10] = "FD 25";
        let demo[11] = "RT 90";
        let demo[12] = "FD 25";
        let demo[13] = "BK 50";
        let demo[14] = "LT 90";
        let demo[15] = "FD 50";
        let demo[16] = "RT 90";
        let demo[17] = "FD 50";
        let demo[18] = "LT 90";
        let demo[19] = "FD 25";
        let demo[20] = "RT 90";
        let demo[21] = "FD 25";
        let demo[22] = "RT 90";
        let demo[23] = "FD 75";
        let demo[24] = "LT 90";
        let demo[25] = "FD 25";
        let demo[26] = "LT 90";
        let demo[27] = "FD 100";
        let demo[28] = "LT 90";
        let demo[29] = "FD 100";
        let demo[30] = "LT 90";
        let demo[31] = "FD 50";
        let demo[32] = "BK 100";
        let demo[33] = "LT 90";
        let demo[34] = "FD 50";
        let demo[35] = "RT 90";
        let demo[36] = "FD 50";

        do Output.moveCursor(0, 0);
        while (i < size) {
            let input = demo[i];
            if (i > 0) {
                if (i < 19) {
                    do Output.moveCursor(i, 0);
                } else {
                    do Output.moveCursor(i - 19, 58);
                }
                do Output.printString(input);
                do Sys.wait(500);
            }
            do parseInput(input);
            if (i = 0) { // wait to print CLEAR command only after clearscreen
                do Output.moveCursor(0, 0);
                do Output.printString(input);
                do Sys.wait(500);
            }
            do input.dispose();
            let i = i + 1;
        }

        do demo.dispose();
        let error = false;
        return;
    }

}